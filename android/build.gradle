buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'me.tatarka:gradle-retrolambda:2.2.1'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'retrolambda'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        applicationId "com.fusionx.lightirc"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release
    }

    productFlavors {
        simple {
            applicationId = android.defaultConfig.applicationId
            versionName = android.defaultConfig.versionName
            ext.enableCrashlytics = false
        }
        internal {
            applicationId = android.defaultConfig.applicationId
            versionName = android.defaultConfig.versionName
            ext.enableCrashlytics = true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            signingConfig signingConfigs.release
        }
    }
}
apply plugin: 'android-unit-test'

dependencies {
    compile 'com.android.support:support-v4:19.+'
    compile 'com.android.support:appcompat-v7:19.+'
    compile 'se.emilsjolander:stickylistheaders:2.1.3'
    compile 'com.nhaarman.listviewanimations:library:2.+'
    compile 'com.sothree.slidinguppanel:library:2.0.2'

    compile 'com.crashlytics.android:crashlytics:1.+'

    testCompile 'junit:junit:4.+'
    testCompile 'org.robolectric:robolectric:+'
    testCompile 'org.assertj:assertj-core:1.+'

    compile project(':relay-library')
    compile project(':bus-library')
}

retrolambda {
    jdk System.getenv("JAVA_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}